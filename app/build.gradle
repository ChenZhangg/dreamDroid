buildscript {
    repositories {
        mavenCentral()
        jcenter()
        flatDir{
            dirs 'libs'
        }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
    }
}
apply plugin: 'com.android.application'

repositories {
    google()
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation ('com.afollestad.material-dialogs:core:0.9.6.0') {
        exclude group: 'com.android.support'
        exclude module: 'support-v4'
        exclude module: 'preference-v7'
        exclude module: 'preference-v14'
    }
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:support-v4:27.+'
    implementation 'com.android.support:preference-v14:27.+'
    implementation 'com.android.support:preference-v7:27.+'
    implementation 'com.android.support:design:27.+'
    implementation 'com.android.support:recyclerview-v7:27.+'
    implementation 'com.android.support:cardview-v7:27.+'
    implementation 'com.android.support:leanback-v17:27.+'
    implementation 'com.android.support:preference-leanback-v17:27.+'
    implementation 'com.android.support:support-annotations:27.+'
    implementation "com.butterproject:vlc-android-sdk:3.0.+"
    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.18'
    implementation 'biz.kasual:materialnumberpicker:1.2.1'
    implementation 'com.github.chrisbanes:PhotoView:2.0.+'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation ('com.wdullaer:materialdatetimepicker:3.6.0'){
        exclude group: 'com.android.support'
        exclude module: 'support-v4'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v13'
    }
    implementation 'commons-net:commons-net:3.6'
    implementation 'org.openhab.jmdns:jmdns:3.4.2'
    implementation 'org.piwik.sdk:piwik-sdk:3.0.2'
    implementation project(':libraries:ckChangeLog')
    implementation project(':libraries:gaugeview-library')
    implementation project(':libraries:MemorizingTrustManager')
}

android {
    compileSdkVersion Integer.parseInt(project.COMPILE_SDK_VERSION)
    buildToolsVersion project.BUILD_TOOLS_VERSION

    defaultConfig {
        versionCode 415
        versionName="1.2.415"
        multiDexEnabled true
        sourceSets {
            main {
                manifest.srcFile 'AndroidManifest.xml'
                java.srcDirs = ['src']
                res.srcDirs = ['res']
            }
        }
    }
    flavorDimensions "distribution"
    productFlavors {
        google {
            dimension "distribution"
            applicationId "net.reichholf.dreamdroid"
        }
        fdroid {
            dimension "distribution"
            applicationId "net.reichholf.dreamdroid"
        }
        amazon {
            dimension "distribution"
            applicationId "net.reichholf.dreamdroid.amazon"
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a' //select ABIs to build APKs for
            universalApk true //generate an additional APK that contains all the ABIs
        }
    }

    project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 5, 'mips64': 6, 'x86': 8, 'x86_64': 9]

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(
                            com.android.build.OutputFile.ABI), 0) * 10000000 + android.defaultConfig.versionCode
        }
    }

    buildTypes {
        debug {
            // according to https://developer.android.com/studio/build/gradle-tips changing BuildConfig on every build iterferes with instantrun
            // disable it for debug builds
            buildConfigField "long", "BUILD_TIME", "0L"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            // Add build date object to BuildConfig
            buildConfigField "long", "BUILD_TIME", "" + System.currentTimeMillis() + "L"
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
